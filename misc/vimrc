
function! s:insertIncludeGuard()
    let fullname = toupper(expand('%'))
    let reducedname = substitute(fullname, 'src\/', '', '')
    let gatename = substitute(substitute(reducedname, '\.', '_', 'g'), '\/', '_', 'g')
    execute "normal! i#ifndef TSYM_" . gatename
    execute "normal! o#define TSYM_" . gatename . "\n"
    execute "normal! Go#endif"
    normal! kk
endfunction

autocmd BufNewFile *.{h,hpp} call <SID>insertIncludeGuard()
autocmd BufRead,BufReadPost README.md set spell spelllang=en_us

call add(g:UltiSnipsSnippetDirectories, getcwd() . '/misc')

set path=bin/**,misc/**,src/**,test/**

function! GetTestCaseAndGroup()
    let l:pattern = '^ *TEST *( *\([^ ]\+\) *, *\([^ ]\+\) *) *$'
    let l:nLine = line('.')

    while getline(l:nLine) !~ l:pattern
        let l:nLine = l:nLine - 1
        if l:nLine == 0
            return ['', '']
        endif
    endwhile

    let l:match = matchlist(getline(l:nLine), l:pattern)
    return l:match[1:2]
endfunction

function! RunTestGroupCursorIsIn()
    let l:group = GetTestCaseAndGroup()[0]
    let l:cmd = '!runtests -c -sg ' . l:group
    execute l:cmd
endfunction

function! RunTestCaseCursorIsIn()
    let l:group = GetTestCaseAndGroup()[0]
    let l:test = GetTestCaseAndGroup()[1]

    let l:cmd = '!runtests -c -sg ' . l:group . ' -sn ' . l:test
    execute l:cmd
endfunction

map <F8> :up<CR>:exe ":make! build/" . substitute(expand('%'), '\.[hc]p*', '.o', '')<CR>:cw<CR>
imap <F8> <ESC><F8>
map <F10> :argadd `git ls-files '*.h' '*.cpp'`
map <F11> :call RunTestCaseCursorIsIn()<CR>
map <F12> :call RunTestGroupCursorIsIn()<CR>
